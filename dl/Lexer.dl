
Position struct {
    line i32
    column i16
    file i16
}

Token struct
{
	Type i32
	value Str
	at Position
}

Lexer struct {
	nextCharacter i32
	nextNextCharacter i32

	tokens Array(Token)
	at i64

	currentLine i64
	currentColumn i64
	currentFile i64

	dataIndex i64
	data ref i8
}

kParenOpen i32 = Char('(')
kParenClose i32 = Char(')')

kBracketOpen i32 = Char('{')
kBracketClose i32 = Char('}')

kBraceOpen i32 = Char('[')
kBraceClose i32 = Char(']')

kPlus i32 = Char('+')
kMinus i32 = Char('-')
kDivide i32 = Char('/')
kMultiply i32 = Char('*')
kMod i32 = Char('%')

kEqual i32 = Char('=')
kNot i32 = Char('!')
kGreater i32 = Char('>')
kLess i32 = Char('<')

kComma i32 = Char(',')
kDot i32 = Char('.')

kCaret i32 = Char('^')
kDollar i32 = Char('$')
kOption i32 = Char('?')
kAnd i32 = Char('&')
kOr i32 = Char('|')

kAt i32 = Char('@')

kEnd i32 = 0
kSemicolon i32 = Char(';')
kEndLine i32 = 0-26


kKeywordsBegin i32 = 0-100 			//Keywords end

kStruct i32 = kKeywordsBegin + 1
kFunction i32 = kKeywordsBegin + 2
kRef i32 = kKeywordsBegin + 3
kEnum i32 = kKeywordsBegin + 4

kTrait i32 = kKeywordsBegin + 5
kRequire i32 = kKeywordsBegin + 6
kFor i32 = kKeywordsBegin + 7
kBreak i32 = kKeywordsBegin + 8
kContinue i32 = kKeywordsBegin + 9
kIf i32 = kKeywordsBegin + 10
kElse i32 = kKeywordsBegin + 11
kReturn i32 = kKeywordsBegin + 12

kKeywordsEnd i32 = 0-50	

kIdentifier i32 = 0-20
kString i32 = 0-21
kNumber i32 = 0-22

TokenString fn token i32 => Str {
	if token == kNumber {
		return 'number'
	}
	if token == kStruct {
		return 'struct'
	}
	if token == kIdentifier {
		return 'identifier'
	}
	if token == kString {
		return 'string'
	}
	if token == kRef {
		return 'ref'
	}
	if token == kFunction {
		return 'fn'
	}
	if token == kTrait {
		return 'trait'
	}
	if token == kEndLine {
		return 'end of line'
	}
	if token == kIf {
		return 'if'
	}
	if token == kElse {
		return 'else'
	}

	return StringFromChar(token.i8())
}

== fn token Token, kind i64 => i8 {
	return token.Type == kind
}
!= fn token Token, kind i64 => i8 {
	return token.Type != kind
}

Lexer fn file Str, fileNumber i64 => Lexer
{
	self Lexer
	self.currentLine = 1
	self.currentColumn = 0
	self.currentFile = fileNumber

	f ref FileHandle = FileOpen(file.chars, 'r'.chars)


	if f == Nil {
		Print('Failed to open file \'' + file + '\'')
		return self
	}

	self.data = Alloc(i8, 100000)
    newLen u64 = FileRead(self.data, 1, 100000, f)
    FileClose(f)

	token Token = self.Next() 
	for token.Type != 0 && token.Type != 0-1
	{
		self.tokens.Push(token)
		token = self.Next() 
	}

	none Token
	self.tokens.Push(none)
	self.tokens.Push(none)
	self.tokens.Push(none)

	return self
}



Peek fn self ref Lexer => i32
{
	return self.data.PtrAdd(self.dataIndex).i32
}
Peek fn self ref Lexer, at i64 => i32
{
	return self.data.PtrAdd(self.dataIndex + at).i32
}
Get fn self ref Lexer => i32
{

	character i32 = self.Peek()
	self.dataIndex = self.dataIndex + 1
	self.currentColumn = self.currentColumn + 1
	return character
}

@extern
{
	isspace fn v i32 => i32
	isalpha fn v i32 => i32
	isalnum fn v i32 => i32
	isdigit fn v i32 => i32
}

At fn self ref Lexer, i i64 => Token
{
	return self.tokens[self.at + i]
}
Eat fn self ref Lexer => {
	self.at = self.at + 1
}
Eat fn self ref Lexer, count i64 => {
	self.at = self.at + count
}

Next fn self ref Lexer => Token
{
	insertNewLine i8
	for isspace(Peek(self)) != 0
	{
		if Get(self) == Char('\n')
		{
			self.currentLine = self.currentLine + 1
			self.currentColumn = 0
			insertNewLine = 1
		}
	}

	if insertNewLine && self.tokens.length > 0 && Peek(self) != Char('{') && Peek(self) != Char('}'){

		t i32 = self.tokens[self.tokens.length-1].Type
		if t == kParenClose || t == kBraceClose || t == kIdentifier || t == kNumber || t == kString || (t >= kKeywordsBegin && t < kKeywordsEnd)
		{
			t Token
			t.Type = kEndLine
			t.at.line = self.currentLine.i32()
			t.at.file = self.currentFile.i16()
			t.at.column = self.currentColumn.i16()
			self.tokens.Push(t)
		}
	}



	if (isalpha(Peek(self)) == true || Peek(self) == Char('_'))
	{
		t Token
		t.at.line = self.currentLine.i32()
		t.at.file = self.currentFile.i16()
		t.at.column = self.currentColumn.i16()

		data Array(i8)
		Init(data)
		for isalnum(Peek(self)) == true || Peek(self) == Char('_'){
			Push(data, i8(Get(self)))
		}
		Push(data, 0)

	 	
		t.Type = kIdentifier
		t.value.chars = data.elements
		t.value.length = data.length - 1

		if 	 t.value == 'struct'	{ t.Type = kStruct }
		else t.value == 'fn'		{ t.Type = kFunction }
		else t.value == 'enum'		{ t.Type = kEnum }
		else t.value == 'ref'		{ t.Type = kRef }
		else t.value == 'trait'		{ t.Type = kTrait }
		else t.value == 'require'	{ t.Type = kRequire }
		else t.value == 'for'		{ t.Type = kFor }
		else t.value == 'break'		{ t.Type = kBreak }
		else t.value == 'continue'	{ t.Type = kContinue }
		else t.value == 'if'		{ t.Type = kIf }
		else t.value == 'else'		{ t.Type = kElse }
		else t.value == 'return'	{ t.Type = kReturn }
		return t
	}

	if isdigit(Peek(self)) == true
	{

		data Array(i8)
		Init(data)

		if Peek(self) == Char('0') && Peek(self, 1) == Char('x') {
			Push(data, i8(Get(self)))
			Push(data, i8(Get(self)))

			for isdigit(Peek(self)) == true || 
				Peek(self) == Char('A') ||
				Peek(self) == Char('B')	||
				Peek(self) == Char('C') ||
				Peek(self) == Char('D') ||
				Peek(self) == Char('E') ||
				Peek(self) == Char('F')  
			{
				Push(data, i8(Get(self)))
			}
		}
		else 
		{
			num f64 = (0)
			m f64 = (10)

			for isdigit(Peek(self)) == true 
			{
				Push(data, i8(Get(self)))
			}
			if Peek(self) == Char('.') && isdigit(Peek(self, 1)) == true
			{
				Push(data, i8(Get(self)))
				for isdigit(Peek(self)) != 0{
					Push(data, i8(Get(self)))
				}
			}
		}

	
		Push(data, 0)

		t Token
		t.Type = kNumber
		t.value.chars = data.elements
		t.value.length = data.length - 1
		t.at.line = self.currentLine.i32()
		t.at.file = self.currentFile.i16()
		t.at.column = self.currentColumn.i16()
		return t
	}
	if Peek(self) == Char('\'')
	{
		Get(self)//Consume '

		data Array(i8)
		Init(data)

		for Peek(self) != Char('\'') {
			char i32 = Get(self)
			if char == 92 {
				next i32 = Get(self)
				if next == Char('t'){
					Push(data, i8(9))
				}
				else next == Char('n'){
					Push(data, i8(Char('\n')))
				}
				else next == Char('0'){
					Push(data, i8(0))
				}
				else next == Char('\''){
					Push(data, i8(Char('\'')))
				}
				else next == 92 {
					Push(data, i8(92))
				}
			}
			else {
				Push(data, char.i8)
			}
		}

		Push(data, 0)
		char i32 = Get(self)//Consume '
		assert(char == Char('\''))

		t Token
		t.Type = kString
		t.value.chars = data.elements
		t.value.length = data.length - 1
		t.at.line = self.currentLine.i32()
		t.at.file = self.currentFile.i16()
		t.at.column = self.currentColumn.i16()
		return t
	}
	t Token
	t.Type = (Get(self))

	if t.Type == Char('/') && Peek(self) == Char('/') {
		self.Get()
		for Peek(self) != Char('\n') &&  Peek(self) != 0 {
			self.Get()
		}
		return self.Next()
	}

	chars ref i8 = Alloc(i8, 2)
	chars.PtrAdd(0).dref = t.Type.i8()
	chars.PtrAdd(1).dref = 0

	t.value = String(chars)
	t.at.line = self.currentLine.i32()
	t.at.file = self.currentFile.i16()
	t.at.column = self.currentColumn.i16()
	return t
}
