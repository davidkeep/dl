
@import {
	'dl/Array.dl',
	'dl/String.dl',
    'Lexer.dl',
    'Ast.dl',
    'Print.dl',
    'Semantic.dl',
    'Intrinsic.dl',
    'Generate.dl',
    'Project.dl',
    'Interp.dl',
    'GenerateInterp.dl',
}

Error struct {
    message str
    at Position
}

errors DynamicArray(Error)

ParseBasicBlock fn(lexer ref Lexer) Block^ {
    lexer.Expect(kBracketOpen)
    block Block^ = Block()
    for true
    {
        expr Expr^
        if !expr {
            expr = &lexer.ParseVariable().super
        }
        if !expr {
            expr = lexer.ParseExpression()
        }
        if !expr {
            expr = lexer.ParseControl()
        }
        if !expr
        {
            lexer.Expect(kBracketClose)
            return block
        }
        block.Add(expr)
    }
    return block
}
ParseDirective fn (lexer ref Lexer, block ref Block) {
    for lexer.At(0) == kAt 
    {
        lexer.Eat(1)

        if lexer.At(0) == kIdentifier {

            if lexer.At(0).value == 'extern' {

                lexer.Eat(1)
                lexer.Expect(kBracketOpen)

                for true
                {
                    expr Expr^
                    expr = &lexer.ParseFunction().super
                    if expr {
                        block.Add(expr)
                    }
                    else
                    {
                        lexer.Expect(kBracketClose)
                        return
                    }
                }
            }
            else if lexer.At(0).value == 'import' {

                lexer.Eat(1)
                lexer.Expect(kBracketOpen)

                for lexer.At(0) == kString
                {
                    filesToParse.Push(lexer.At(0).value)
                    lexer.Eat(1)
                    lexer.ExpectEndStatement()
                }
                lexer.Expect(kBracketClose)
            }
            else {
                Assert(0, 'Unhandled directive  ' + lexer.At(0).value)
            }

        }
        else {
            Error('Expected identifier', lexer.At(0).at)
        }
    }
}

ParseFunction fn (lexer ref Lexer) Function^
{
    if lexer.At(1) == kFunction && (
            lexer.At(0) == kIdentifier ||
            lexer.At(0) == kMultiply ||
            lexer.At(0) == kPlus     ||
            lexer.At(0) == kMod     ||
            lexer.At(0) == kLess     ||
            lexer.At(0) == kGreater
        )
    {
        self Function^ = Function()
        self.super.at = lexer.At(0).at
        if lexer.At(0) == kIdentifier {
            self.ident = lexer.At(0).value
        }
        else {
            self.ident = TokenString(lexer.At(0).Type)
        }
        lexer.Eat(2)

        done i8
        for !done {

            expr Expr^ = &lexer.ParseParameter().super
            if expr {
                self.params.Add(expr)
                if lexer.At(0) == kComma 
                {
                    lexer.Eat(1)
                }
                else {
                    done = true
                }
            }
            else {
                done = true
            }
        }

        if lexer.At(0) == kEqual && lexer.At(1) == kGreater {
            lexer.Eat(2)
            self.result = lexer.ParseType()
        }
        if lexer.At(0) != kBracketOpen {
            lexer.ExpectEndStatement()
            return self
        }
        self.block = lexer.ParseBasicBlock()
        return self
    }
    if lexer.At(2) == kFunction && 
        (
            (lexer.At(0) == kEqual && lexer.At(1) == kEqual) ||
            (lexer.At(0) == kNot && lexer.At(1) == kEqual)  ||
            (lexer.At(0) == kBraceOpen && lexer.At(1) == kBraceClose)
        )
    {
        self Function^ = Function()
        self.super.at = lexer.At(0).at
        if lexer.At(0) == kEqual {
            self.ident = '=='
        }
        else if lexer.At(0) == kNot {
            self.ident = '!='
        }
        else if lexer.At(0) == kBraceOpen {
            self.ident = '[]'
        }
        else {
            Assert(0, 'no function found')
        }
        lexer.Eat(3)

        done i8
        for !done {

            expr Expr^ = &lexer.ParseParameter().super
            if expr {
                self.params.Add(expr)
                if lexer.At(0) == kComma 
                {
                    lexer.Eat(1)
                }
                else {
                    done = true
                }
            }
            else {
                done = true
            }
        }

        if lexer.At(0) == kEqual && lexer.At(1) == kGreater {
            lexer.Eat(2)
            self.result = lexer.ParseType()
        }
        if lexer.At(0) != kBracketOpen {
            lexer.ExpectEndStatement()
            return self
        }
        self.block = lexer.ParseBasicBlock()
        return self
    }
    return cast(Function^, 0)
}

ParseStructure fn (lexer ref Lexer) Structure^
{
    if lexer.At(1) == kStruct && lexer.At(0) == kIdentifier
    {
        structure Structure^ = Structure()
        structure.ident = lexer.At(0).value
        lexer.Eat(2)

        lexer.ParseConstraints(structure.constraints)

        if lexer.At(0) == kSemicolon || lexer.At(0) == kEndLine {
            lexer.Eat(1)
            structure.incomplete = true
            return structure
        }

        lexer.Expect(kBracketOpen)

        for lexer.At(0) != kBracketClose {
            field Variable^ = lexer.ParseField()
            structure.Add(field)
        }

        lexer.Expect(kBracketClose)

        return structure
    }

    return cast(Structure^, 0)
}

// (<Type>, $<Type>, ...)
ParseConstraints fn (lexer ref Lexer, constraints ref DynamicArray(Type^))
{

    for lexer.At(0) != kBracketOpen {
        typ Type^ = lexer.ParseType()
        if !typ {
            Error('Expected { or type following struct', lexer.At(0).at)
            return
        }
        constraints.Push(typ)
        if lexer.At(0) != kBracketOpen
        {
            lexer.Expect(kComma)
        }
    }
}

ParseTrait fn (lexer ref Lexer) Trait^
{
    if lexer.At(1) == kTrait && lexer.At(0) == kIdentifier
    {
        elTrait Trait^ = New(Trait)
        elTrait.ident = lexer.At(0).value
        lexer.Eat(2)

        lexer.ParseConstraints(elTrait.constraints)
        lexer.Expect(kBracketOpen)

        for lexer.At(0) != kBracketClose 
        {
            function Function^ = lexer.ParseFunction()
            elTrait.AddFunction(function)
        }

        lexer.Expect(kBracketClose)

        return elTrait
    }

    return cast(Trait^, 0)
}

ParseRequire fn (lexer ref Lexer) Require^
{
    if lexer.At(1) == kRequire && lexer.At(0) == kIdentifier
    {
        req Require^ = New(Require)
        req.typeName = lexer.At(0).value
        lexer.Eat(2)
        if lexer.At(0) != kIdentifier
        {
            Error('Expected identifier after ident require', lexer.At(0).at)
        }
        else {
            req.traitName = lexer.At(0).value
            lexer.Eat(1)
        }
        lexer.ExpectEndStatement()
        return req
    }
    return cast(Require^, 0)
}

ParseOptions fn(lexer ref Lexer, typ Type^) Type^ {
    t Type^ = typ
    for lexer.At(0) == kOption {
        lexer.Eat(1)
        opt TypeOption^ = New(TypeOption)
        opt.reference = t
        t = &opt.super
    }
    return t
}
ParseType fn(lexer ref Lexer) Type^ {

    if lexer.At(0) == kIdentifier {
        if lexer.At(0).value == 'typeof' {
            typ TypeType^ = New(TypeType)
            lexer.Eat(1)
            typ.reference = lexer.ParseType()
            return lexer.ParseOptions(&typ.super)
        }
        else {
            typ TypeIdentifier^ = TypeIdentifier()
            typ.ident = lexer.At(0).value
            lexer.Eat(1)

            if lexer.At(0) == kParenOpen {
                generic TypeGeneric^ = New(TypeGeneric)
                generic.ident = typ
                lexer.Eat(1) // eat (

                for true
                {
                    generic.constraints.Push(lexer.ParseType())
                    if lexer.At(0) == kComma {
                        lexer.Eat(1)
                    }
                    else
                    {
                        lexer.Expect(kParenClose)
                        return lexer.ParseOptions(&generic.super)
                    }
                }
            }
            return lexer.ParseOptions(&typ.super)
        }
    }
    if lexer.At(0) == kRef {
        typ TypeRef^ = New(TypeRef)
        lexer.Eat(1)
        r Type^ = lexer.ParseOptions(&typ.super)
        typ.reference = lexer.ParseType()
        return r
    }
    if lexer.At(0) == kDollar {
        lexer.Eat(1)
        if lexer.At(0) != kIdentifier {
            Error('Expected identifier after $', lexer.At(0).at)
        }
        typ TypeAny^ = New(TypeAny)
        typ.ident = lexer.At(0).value
        lexer.Eat(1)

        return lexer.ParseOptions(&typ.super)
    }
   
    return cast(Type^, 0)
}

ParseField fn(lexer ref Lexer) Variable^ {
    
    field Variable^ = Variable()
    field.ident = lexer.At(0).value
    lexer.Eat(1)

    field.super.typ = lexer.ParseType()
    if !field.super.typ {
        Error('Expected type of field', lexer.At(0).at)
    }
    lexer.ExpectEndStatement()
    return field
}

ParseParameter fn(lexer ref Lexer) Variable^ {
    
    if lexer.At(0) == kIdentifier && (
        lexer.At(1) == kIdentifier || 
        lexer.At(1) == kRef ||
        lexer.At(1) == kDollar
    )
    {
        variable Variable^ = Variable()
        variable.super.at = lexer.At(0).at
        if !(lexer.At(0).value == 'typeof') 
        {
            variable.ident = lexer.At(0).value
            lexer.Eat(1)
        }
        variable.super.typ = lexer.ParseType()
        return variable
    }

    return cast(Variable^, 0)
}

ParseVariable fn(lexer ref Lexer) Variable^ {
    
        if lexer.At(0) == kIdentifier && (
            lexer.At(1) == kIdentifier || 
            lexer.At(1) == kRef ||
            lexer.At(1) == kDollar
        )
        {
            variable Variable^ = Variable()
            variable.super.at = lexer.At(0).at
            if !(lexer.At(0).value == 'typeof') 
            {
                variable.ident = lexer.At(0).value
                lexer.Eat(1)
            }

            variable.super.typ = lexer.ParseType()
            if lexer.At(0) == kEqual {
                lexer.Eat(1)
                variable.assign = lexer.ParseExpression(0)
                if !variable.assign {
                    Error('Expected expression following =')
                }
            }
            lexer.ExpectEndStatement()
            return variable
        }

    return cast(Variable^, 0)
}
ParseControl fn(lexer ref Lexer) Expr^
{
    if lexer.At(0) == kFor {
        lexer.Eat(1)

        expr Expr^ = lexer.ParseExpression(0)
        if !expr
        {
            Error('Expected expression following for', lexer.At(0).at)
            return cast(Expr^, 0)
        }  
        if expr.Is(ExpressionList) {

            self ForList^ = New(ForList)
            self.list = expr.As(ExpressionList)
            self.block = lexer.ParseBasicBlock()
            return &self.super
        }
        self For^ = New(For)
        self.from = expr

        if lexer.At(0) == kDot && lexer.At(1) == kDot {
            lexer.Eat(2)
            self.to = lexer.ParseExpression(0)
            if !self.to
            {
                Error('Expected expression following for expr .. ', lexer.At(0).at)
                return cast(Expr^, 0)
            }  
        }

        self.block = lexer.ParseBasicBlock()
        return &self.super
    }
    if lexer.At(0) == kBreak || lexer.At(0) == kContinue {
        self Branch^ = New(Branch)
        self.token = lexer.At(0).Type
        lexer.Eat(1)
        return &self.super
    }
    if lexer.At(0) == kIf {
        self If^ = New(If)
        lexer.Eat(1)

        self.condition = lexer.ParseExpression(0)
        self.trueBranch = lexer.ParseBasicBlock()
        current If^ = self

        for lexer.At(0) == kElse {
            lexer.Eat(1)

            if lexer.At(0) == kBracketOpen {
                current.falseBranch = lexer.ParseBasicBlock()
            }
            else {
                current.falseCondition = New(If)
                current.falseCondition.trueBranch = lexer.ParseBasicBlock()
                current = current.falseCondition
            }
        }
        return &self.super
    }
    if lexer.At(0) == kReturn {
        self Return^ = New(Return)
        self.super.at = lexer.At(0).at
        lexer.Eat(1)
        self.expr = lexer.ParseExpression()
        return &self.super
    }

    return cast(Expr^, 0)
}
ParseOperand fn(lexer ref Lexer, precedence i32) Expr^
{
    if lexer.At(0) == kIdentifier {
        operand Identifier^ = Identifier()
        operand.ident = lexer.At(0).value
        lexer.Eat(1)
        return &operand.super
    }

    if lexer.At(0) == kNumber {
        operand NumberConstant^ = New(NumberConstant)
        operand.value = lexer.At(0).value
        lexer.Eat(1)
        return &operand.super
    }
    if lexer.At(0) == kString {
        operand StringConstant^ = New(StringConstant)
        operand.value = lexer.At(0).value
        lexer.Eat(1)
        return &operand.super
    }
    if lexer.At(0) == kAddress {
        op Call^ = Call()
        identifier Identifier^ = Identifier()
        identifier.ident = '&'
        op.operand = &identifier.super
        lexer.Eat(1)    
        op.params.Add(lexer.ParseExpression(0))
        return &op.super
    }
    if lexer.At(0) == kParenOpen {
        lexer.Eat(1)
        expr Expr ^ = lexer.ParseExpression(0)
        if !expr {
            Error('Expected expression following (', lexer.At(0).at)
        }
        lexer.Expect(kParenClose)
        return expr
    }
    //if lexer.At(0) == kDref {
    //    op Call^ = Call()
    //    identifier Identifier^ = Identifier()
    //    identifier.ident = 'dref'
    //    op.operand = &identifier.super
    //    lexer.Eat(1)    
    //    op.params.Add(lexer.ParseExpression(0))
    //    return &op.super
    //}
    if lexer.At(0) == kBraceOpen {
        operand ExpressionList^ = New(ExpressionList)
        lexer.Eat(1)

        for true {
            expr Expr^ = lexer.ParseExpression(0)
            if expr {
                operand.list.Push(expr)
                if lexer.At(0) == kComma {
                    lexer.Eat(1)
                }
                else {
                    lexer.Expect(kBraceClose)
                    return &operand.super
                }
            }
            else {
                lexer.Expect(kBraceClose)
                return &operand.super
            }
        }
        
       
        return &operand.super
    }
    typ Type^ = lexer.ParseType()
    if typ {
        //Assert(0, 'AHhahah a type')
        ident Identifier^ = New(Identifier)
        as TypeType^ = New(TypeType)
        as.reference = typ
        ident.super.typ = &as.super 
        return &ident.super
    }


    return cast(Expr^, 0)
}

IsOperator fn(token Token) i8 {
    return token == kPlus || token == kMinus || token == kMultiply || token == kDivide || token == kMod
}

Precedence fn(token Token) i32 {
    //if token == kOrOr {
    //    return 1
    //}
    //if token == kAndAnd {
    //    return 2
    //}

    //if token == kEqual || token == kLess  || token == kLessEq  || token == kGreater  || token == kNotEqual 
    //{
    //    return 3
    //}

    if token == kPlus || token == kMinus //  || token == kAnd  || token == kOr 
    {
        return 4
    }

    if token == kMultiply || token == kDivide  || token == kMod 
    {
        return 5
    }

    Assert(0, 'Uknown precedence for token, ' +  TokenString(token.Type))
    return 0
}

CreateCall fn(ident str, left Expr^, right Expr^) Expr^ {
    op Call^ = Call()
    identifier Identifier^ = Identifier()
    identifier.ident = ident
    op.operand = &identifier.super
    op.params.Add(left)
    op.params.Add(right)
    return &op.super
}

ParseExpression fn(lexer ref Lexer, precedence i32) Expr^
{
    left Expr^ = ParseOperand(lexer, precedence)
    for left
    {
        if lexer.At(0) == kDot && lexer.At(1) != kDot {
            lexer.Eat(1)
            access Access^ = New(Access)
            access.operand = left

            if lexer.At(0) != kIdentifier {
                Assert(0, 'Expected identifier following .',)
            }
            access.field = lexer.At(0).value
            lexer.Eat(1)
            
            left = &access.super
        }
        else if lexer.At(0) == kEqual && lexer.At(1) != kEqual {
            assign Assign^ = New(Assign)
            assign.super.at = lexer.At(0).at
            lexer.Eat(1)
            assign.left = left
            assign.right = lexer.ParseExpression(0)
            if !assign.right {
                Assert(0, 'Expected expression following .',)
            }
            left = &assign.super
        }
        else if lexer.At(0) == kEqual {
            if 3 < precedence {
                return left
            }
            lexer.Eat(2)
            left = CreateCall('==', left, ParseExpression(lexer, precedence + 1))
        }
        else if lexer.At(0) == kNot && lexer.At(1) == kEqual {
            if 3 < precedence {
                return left
            }
            lexer.Eat(2)
            left = CreateCall('!=', left, ParseExpression(lexer, precedence + 1))
        }
        else if lexer.At(0) == kBraceOpen {
            lexer.Eat(1)
            left = CreateCall('[]', left, ParseExpression(lexer, 0))
            lexer.Expect(kBraceClose)
        }
        else if lexer.At(0) == kGreater && lexer.At(1) == kEqual {
            if 3 < precedence {
                return left
            }
            lexer.Eat(2)
            left = CreateCall('>=', left, ParseExpression(lexer, precedence + 1))
        }
        else if lexer.At(0) == kGreater {
            if 3 < precedence {
                return left
            }
            lexer.Eat(1)
            left = CreateCall('>', left, ParseExpression(lexer, precedence + 1))
        }
        else if lexer.At(0) == kLess && lexer.At(1) == kEqual {
            if 3 < precedence {
                return left
            }
            lexer.Eat(2)
            left = CreateCall('<=', left, ParseExpression(lexer, precedence + 1))
        }
        else if lexer.At(0) == kLess {
            if 3 < precedence {
                return left
            }
            lexer.Eat(1)
            left = CreateCall('<', left, ParseExpression(lexer, precedence + 1))
        }
        else if lexer.At(0) == kEqual {
            lexer.Eat(2)
            op Call^ = Call()
            identifier Identifier^ = Identifier()
            identifier.ident = '=='
            op.operand = &identifier.super
            op.params.Add(left)
            left = &op.super
        }
        else if lexer.At(0).IsOperator() {

            nextPrecedence i32 = lexer.At(0).Precedence()

            if (nextPrecedence < precedence) 
            {
                return left
            }

            op Call^ = Call()
            identifier Identifier^ = Identifier()
            identifier.ident = TokenString(lexer.At(0).Type)
            op.operand = &identifier.super
            lexer.Eat(1)
        
            right Expr^ = ParseExpression(lexer, nextPrecedence + 1)
            Assert(left, 'Expected left')
            Assert(right, 'Expected right')

            op.params.Add(left)
            op.params.Add(right)
            left = &op.super
        }
        else if lexer.At(0) == kParenOpen
        {
            call Call^ = Call()
            call.operand = left
            lexer.Eat(1)

            for lexer.At(0) != kParenClose {
                expr Expr^ = lexer.ParseExpression(0)
                if !expr {
                    Error('Expected )', lexer.At(-1).at)
                    return left
                }
                call.params.Add(expr)
                if lexer.At(0) == kComma {
                    lexer.Eat(1)
                }
            }
            lexer.Expect(kParenClose)
            left = &call.super
        }
        else {
             return left
        }
    }
    return left
}

ParseExpression fn(lexer ref Lexer) Expr^
{
    expr Expr^ = ParseExpression(lexer, 0)
    if expr {
        lexer.ExpectEndStatement()
    }
    return expr
}

Error fn(message str, at Position)
{
    error Error
    error.message = message
    error.at = at
    errors.Push(error)
}

Expect fn(lexer ref Lexer, token i32) Position
{
    at Position = lexer.At(0).at

    if lexer.At(0) != token
    {
        Error('Expected ' + TokenString(token) + ', found ' + TokenString(lexer.At(0).Type), at)
    }
    lexer.Eat(1)
    return at
}

ExpectEndStatement fn(lexer ref Lexer)
{
    if lexer.At(0) == kComma  || lexer.At(0) == kParenClose || lexer.At(0) == kBracketClose || lexer.At(0) == kBracketOpen {
        return
    }
    if lexer.At(0) != kSemicolon && lexer.At(0) != kEndLine
    {
        Error('Expected ; found ' + TokenString(lexer.At(0).Type) + ' ' + String((lexer.At(-1).Type)), lexer.At(0).at)
    }
    lexer.Eat(1)
}

Parse fn(block Block^, file str)
{
    module Module^ = project.AddFile(file)
    lexer Lexer = Lexer(file, project.files.length)
    for true 
    {
        ParseDirective(lexer, ^block)

        expr Expr^
        if !expr {
           expr = &ParseStructure(lexer).super
        }
        if !expr {
           expr = &ParseFunction(lexer).super
        }
        if !expr {
           expr = &ParseTrait(lexer).super
        }
        if !expr {
           expr = &ParseRequire(lexer).super
        }
        if !expr 
        {
            if lexer.At(0) != kEnd
            {
                Error('Expected fn, struct, trait, or variable at top level' + lexer.At(0).value, lexer.At(0).at)
            }
            return
        }
        block.Add(expr)
    }
}

project Project
filesToParse DynamicArray(str)

Compile fn(file str) Block^ 
{
    block Block^ = New(Block)
    filesToParse.Push(file)
    
    for filesToParse.length {
        // Pop the lst file first since we will be adding additional files to the end of this array
        // We don't want to Pop a file that was added during Parse
        theFile str = filesToParse.Last()
        filesToParse.Pop()

        if !project.ContainsFile(theFile) {

            project.AddFile(theFile)
            Parse(block, theFile)
            if errors.length {
                return block
            }
        }
    }

    return block
}

PrintError fn(at Position, message str) {

    f str
    if at.file == 0 {
        f = ''
    }
    else {
        f = project.files[at.file-1].path
    }

    Error('••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••')
    Println(f + ':' + String(at.line) + ':'+ String(at.column) + ': error: ' + message)

    project.PrintLines(at, 6)

    Error('••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••')

}
Main fn() i32 
{
    //Compile('Tests/Extern.dl')
    //Compile('Tests/List.dl')
    //Compile('Tests/For.dl')
    //Compile('Tests/HelloWorldAbc.dl')
    //Compile('Tests/For.dl')
    block Block^ = Compile('Test.dl')

    if errors.length
    {
        PrintError(errors[0].at , errors[0].message)

    }

    if errors.length == 0 {
        semantic Semantic
        semantic.Init(^block)

        if errors.length == 0 {
            //generate Generate
            //generate.Init(semantic, ^block)

            interp Interp
            interp.Init()
            geninterp GenerateInterp
            geninterp.Init(semantic, ^block, interp)
            interp.Run()
        }
        else {
            Println('Semantic error')
        }
    }

    Println('Complete')
    //interp Interp
    //interp.Init()
    //interp.Test()
    return 0
}

